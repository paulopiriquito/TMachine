Name     SerialControl ;
PartNo   02 ;
Date     30/03/2016 ;
Revision 01 ;
Designer Engineer ;
Company  CCISEL ;
Assembly None ;
Location ISEL ;
Device   v750c ;


Pin 1 = SCLK;   //From UsbPort
Pin 2 = SS;     //From UsbPort
Pin 3 = accept; //From Dispatcher
Pin 4 = CLK;    //From ATB

Pin 14 = wr;    //To BitMemory
Pin 15 = DXval;  //To Dispatcher
Pin 16 = busy;  //To UsbPort
Pin [18..21] = [Address0..3]; //To BitMemory


Pin 17 = C0;
Pin 22 = C1;



/*Address Counter*/
/*Current register pointer*/

enable = 'b'1;

[Address0..3].ar = clr;
[Address0..3].sp = 'b'0;
[Address0..3].ck = !SCLK;

Address0.t = enable;
Address1.t = Address0 & enable ;
Address2.t = Address1 & Address0 & enable ;
Address3.t = Address2 & Address1 & Address0 & enable ;

/*Equals 10*/
/*Checks if it's stream's end*/

eq10 = !Address0 & Address1 & !Address2 & Address3;

/*Serial Control */
/*Establishes bit reception protocol*/

[C0..1].ck = CLK;
[C0..1].ar = 'b'0;
[C0..1].sp = 'b'0;

Sequence [C0,C1]{

Present 0:
	out clr;
	if SS next 0;
	default next 1;

Present 1: 
	out clr;
	if !SS next 2; 
	default next 1;

Present 2:
	out wr;
	if !SS next 2;
	if SS & eq10 next 3;
	default next 0;

Present 3:
	out DXval, busy;
	if accept next 0;
	default next 3;
}